WHITESPACE = _{ (" ") }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

Plus = { "+" }
Minus = { "-" }
Multiply = { "*" }
Divide = { "/" }

InfixOperator = _{
    ( Plus
    | Minus
    | Multiply
    | Divide
    )
}

// TODO separate var/fn?
Identifier = @{ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_")* }

Integer = @{ "-"? ~ ASCII_DIGIT+ }
Decimal = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
StringLiteral = @{ "\"" ~ ( "''" | (!"\"" ~ ANY) )* ~ "\"" }
Constant = { (Decimal | Integer | StringLiteral) }

PrimaryExpression =
    {( Constant 
    |  Identifier 
    |  "(" ~ PrimaryExpression ~ ")"
    )} 

PostfixExpression = 
    { Identifier ~ "(" ~ ArgumentExpressionList ~ ")" }

InfixExpression = 
    { PrimaryExpression ~ InfixOperator ~ Expression }

// TODO: Clean this up somehow... 
BlockExpression =
     { "{" ~ 
        // This is like this because otherwise the greedy algorithm
        // will never move on to expect the last expression.
         (!(NEWLINE* ~ Expression ~ NEWLINE* ~ "}") ~ 
            NEWLINE* ~ (VariableDeclaration | Expression))* ~ NEWLINE* ~ 
           Expression ~ NEWLINE*
       ~ "}"
     }

// An Expression is anything that returns a value
Expression = 
    _{( InfixExpression 
    |   PostfixExpression
    |   PrimaryExpression
    |   BlockExpression
    )}

ArgumentExpressionList = 
    { Expression? ~ ("," ~ Expression)* }

VariableDeclaration = 
    { "let " ~ Identifier ~ "=" ~ Expression }

TypedVariable = 
    { Identifier ~ ":" ~ Identifier }

TypedVariableList = 
    { "(" ~ NEWLINE* ~ TypedVariable ? ~ ((","|NEWLINE+) ~ TypedVariable)* ~ NEWLINE* ~ ")" }

TypeDeclaration = 
    {"type " ~ Identifier ~ "=" ~ (TypedVariableList|Identifier) }

FunctionDeclaration = 
    { "fn" ~ Identifier ~ "=" ~ TypedVariableList ~ "->" ~ 
                                 (TypedVariableList|Identifier) ~ 
                                BlockExpression
    }

Statement = 
    _{( FunctionDeclaration
     |  VariableDeclaration 
     |  TypeDeclaration 
     |  Expression) }

Program = _{ SOI ~ "\n"* ~ (Statement ~ "\n"+) * ~ Statement? ~ EOI }
